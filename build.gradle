plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'maven-publish'
}
def libversion = '3.2.1'
version = '3.2.1-4'
def AWS_MVN_BUCKET = System.getenv("AWS_MVN_BUCKET")
def AWS_DEFAULT_REGION = System.getenv("AWS_DEFAULT_REGION")
def AWS_ACCESS_KEY_ID = System.getenv("AWS_ACCESS_KEY_ID")
def AWS_SECRET_ACCESS_KEY = System.getenv("AWS_SECRET_ACCESS_KEY")


repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://dl.bintray.com/gesellix/docker-utils' }
    flatDir { dirs '/usr/share/java' }
    flatDir { dirs 'lib/' }
    flatDir { dirs '/usr/share/java/shadow' }
    if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
        maven {
            url "s3://$AWS_MVN_BUCKET/maven2"
            credentials(AwsCredentials) {
                accessKey AWS_ACCESS_KEY_ID
                secretKey AWS_SECRET_ACCESS_KEY
            }
        }
    }
}
publishing {
    repositories {
        maven {
            url "file:///tmp/maven-repo"
        }
        if (AWS_MVN_BUCKET && !AWS_MVN_BUCKET.allWhitespace) {
            maven {
                url "s3://$AWS_MVN_BUCKET/maven2"
                credentials(AwsCredentials) {
                    accessKey AWS_ACCESS_KEY_ID
                    secretKey AWS_SECRET_ACCESS_KEY
                }
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId 'net.iowntheinter'
            artifactId 'cornerstone'
            from components.java
        }
    }
}

sourceCompatibility = '1.8'
mainClassName = 'net.iowntheinter.vertx.coreLauncher.impl.coreStarter'

dependencies {
    compile "io.vertx:vertx-core:$libversion"
    compile "io.vertx:vertx-lang-groovy:$libversion"
    compile "io.vertx:vertx-shell:$libversion"
    compile "io.vertx:vertx-zookeeper:$libversion"
    compile 'com.englishtown.vertx:vertx-when:4.1.1'
    compile 'com.englishtown.vertx:vertx-hk2:2.2.0'
    compile 'org.apache.curator:apache-curator:2.8.0'
    compile 'org.apache.curator:curator-recipes:2.8.0'
    compile 'org.apache.zookeeper:zookeeper:3.4.6'
    compile 'de.gesellix:docker-client:2016-04-11T22-50-38'
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile 'org.apache.tinkerpop:tinkerpop:3.1.1-incubating'
    compile 'org.apache.tinkerpop:tinkergraph-gremlin:3.1.1-incubating'
}

processResources {
    from 'src/main/groovy'
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes 'Main-Verticle': 'net.iowntheinter.vertx.componentRegister.impl.CoreLauncher'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}
